allprojects {
    group = 'com.ocado.pandateam.newrelic.alerts'
    version = '0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'
}

project(':newrelic-api') {
    dependencies {
        compileOnly 'org.projectlombok:lombok:1.16.10'

        compile 'com.mashape.unirest:unirest-java:1.4.9'
        compile 'com.fasterxml.jackson.core:jackson-databind:2.8.4'
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.8.4'
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.4'
        compile 'commons-io:commons-io:2.5'

        testCompile 'com.google.guava:guava:20.0'
        testCompile 'com.github.tomakehurst:wiremock:2.1.12'
    }
}

project(':newrelic-sync') {
    dependencies {
        compile project(':newrelic-api')
    }
}

subprojects {
    repositories {
        maven { url 'http://maven.ocado.com/nexus/content/repositories/central' }
        mavenLocal()
    }

    dependencies {
        testCompile "junit:junit:4.12"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
        repositories {
            maven {
                credentials {
                    username System.getenv("MAVEN_REPO_USERNAME")
                    password System.getenv("MAVEN_REPO_PASSWORD")
                }
                if (project.version.endsWith('-SNAPSHOT')) {
                    url 'http://maven.ocado.com/nexus/content/repositories/core-services-snapshots/'
                } else {
                    url 'http://maven.ocado.com/nexus/content/repositories/core-services-releases/'
                }
            }
        }
    }
}

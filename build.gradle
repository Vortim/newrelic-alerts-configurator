buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

allprojects {
    group = 'com.ocado.panda.newrelic.alerts'
    version = project.hasProperty('build_version') ? project.getProperty('build_version') : '2.0.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'
}

subprojects {
    repositories {
        maven { url 'http://maven.ocado.com/nexus/content/repositories/central' }
        mavenLocal()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.16.10'
        compile 'org.slf4j:slf4j-api:1.7.21'
        testCompile "junit:junit:4.12"
        testCompile 'com.google.guava:guava:20.0'
        testCompile 'org.mockito:mockito-core:1.10.19'
        testCompile 'com.github.tomakehurst:wiremock:2.1.12'
        testCompile 'org.assertj:assertj-core:3.6.1'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
        repositories {
            maven {
                credentials {
                    username System.getenv("MAVEN_REPO_USERNAME")
                    password System.getenv("MAVEN_REPO_PASSWORD")
                }
                if (project.version.endsWith('-SNAPSHOT')) {
                    url 'http://maven.ocado.com/nexus/content/repositories/core-services-snapshots/'
                } else {
                    url 'http://maven.ocado.com/nexus/content/repositories/core-services-releases/'
                }
            }
        }
    }

    publish.doFirst {
        if (!project.hasProperty("build_version")) {
            throw new IllegalStateException("Missing <build_version> parameter")
        }
    }
}

project(':api') {
    dependencies {
        compile 'commons-io:commons-io:2.5'
        compile 'org.apache.commons:commons-lang3:3.5'
        compile 'org.glassfish.jersey.core:jersey-client:2.24.1'
        compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.24.1'
        compile 'com.fasterxml.jackson.core:jackson-databind:2.8.4'
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.8.4'
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.4'
    }
}

project(':alert-config-uploader') {
    dependencies {
        compile project(':api')
    }
}

project(':deployment-marker') {
    dependencies {
        compile project(':api')
        compile 'org.slf4j:slf4j-simple:1.7.21'
        compile 'org.slf4j:jcl-over-slf4j:1.7.21'
        compile 'com.beust:jcommander:1.48'
    }
    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'
    mainClassName = 'com.ocado.newrelic.deploymentmarker.NewRelicDeploymentCli'
    shadowJar {
        classifier = 'executable'
    }
    build.dependsOn shadowJar
    publishing {
        publications {
            clear()
            shadow(MavenPublication) {
                from components.shadow
            }
        }
    }
}
